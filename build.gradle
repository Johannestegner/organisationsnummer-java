plugins {
    id 'maven-publish'
    id 'java'
    id 'java-library'
}

group 'dev.organisationsnummer'
version = System.getenv("RELEASE_VERSION") ?: 'NONE'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3', 'org.junit.jupiter:junit-jupiter-params:5.10.3', 'org.json:json:20240303'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'

    implementation 'dev.personnummer:personnummer:3.5.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar, jar
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

// Release

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/organisationsnummer/java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        mavenJava (MavenPublication) {
            version = System.getenv("RELEASE_VERSION")
            groupId = 'dev.organisationsnummer'
            from components.java
            pom {
                name = 'Organisationsnummer'
                description = 'Organisationsnummer is a small open-source project created to validate, format and getting the organization type from swedish organization numbers.'
                url = 'https://organisationsnummer.dev'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org/'
                    }
                }
                developers {
                    developer {
                        id = 'jite'
                        name = 'Johannes Tegn√©r'
                        email = 'johannes@jitesoft.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:organisationsnummer/java.git'
                    url = 'https://github.com/organisationsnummer/java'
                }
            }
        }
    }
}
